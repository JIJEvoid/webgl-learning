<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>Title</title>
    <link rel="stylesheet" href="css/main.css">
</head>
<body>
<div id="container"></div>
<script src="js/three.js"></script>

<script id="vertexShader" type="x-shader/x-vertex">
    varying vec2 vUv;
    
    void main() {
        vec4 mvPosition = modelViewMatrix * vec4(position, 1.0);
        
        vUv = uv;
        
        gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
    }

</script>

<script id="fragmentShader" type="x-shader/x-fragment">
    
    precision lowp float;
    
    uniform vec2 iResolution;
    uniform vec2 iMouse;
    uniform float iTime;
    uniform sampler2D  iChannel0;
    // rotate position around axis
    vec2 rotate(vec2 p, float a)
    {
        return vec2(p.x * cos(a) - p.y * sin(a), p.x * sin(a) + p.y * cos(a));
    }
    
    // 1D random numbers
    float rand(float n)
    {
        return fract(sin(n) * 43758.5453123);
    }
    
    // 2D random numbers
    vec2 rand2(in vec2 p)
    {
        return fract(vec2(sin(p.x * 591.32 + p.y * 154.077), cos(p.x * 391.32 + p.y * 49.077)));
    }
    
    // 1D noise
    float noise1(float p)
    {
        float fl = floor(p);
        float fc = fract(p);
        return mix(rand(fl), rand(fl + 1.0), fc);
    }
    
    // voronoi distance noise, based on iq's articles
    float voronoi(in vec2 x)
    {
        vec2 p = floor(x);
        vec2 f = fract(x);
        
        vec2 res = vec2(8.0);
        for(int j = -1; j <= 1; j ++)
        {
            for(int i = -1; i <= 1; i ++)
            {
                vec2 b = vec2(i, j);
                vec2 r = vec2(b) - f + rand2(p + b);
                
                // chebyshev distance, one of many ways to do this
                float d = max(abs(r.x), abs(r.y));
                
                if(d < res.x)
                {
                    res.y = res.x;
                    res.x = d;
                }
                else if(d < res.y)
                {
                    res.y = d;
                }
            }
        }
        return res.y - res.x;
    }
    
    
    
    void main()
    {
        float flicker = noise1(iTime * 2.0) * 0.8 + 0.4;
        
        vec2 uv = gl_FragCoord.xy / iResolution.xy;
        uv = (uv - 0.5) * 2.0;
        vec2 suv = uv;
        uv.x *= iResolution.x / iResolution.y;
        
        
        float v = 0.0;
        
        // that looks highly interesting:
        //v = 1.0 - length(uv) * 1.3;
        
        
        // a bit of camera movement
        uv *= 0.6 + sin(iTime * 0.1) * 0.4;
        uv = rotate(uv, sin(iTime * 0.3) * 1.0);
        uv += iTime * 0.4;
        
        
        // add some noise octaves
        float a = 0.6, f = 1.0;
        
        for(int i = 0; i < 3; i ++) // 4 octaves also look nice, its getting a bit slow though
        {
            float v1 = voronoi(uv * f + 5.0);
            float v2 = 0.0;
            
            // make the moving electrons-effect for higher octaves
            if(i > 0)
            {
                // of course everything based on voronoi
                v2 = voronoi(uv * f * 0.5 + 50.0 + iTime);
                
                float va = 0.0, vb = 0.0;
                va = 1.0 - smoothstep(0.0, 0.1, v1);
                vb = 1.0 - smoothstep(0.0, 0.08, v2);
                v += a * pow(va * (0.5 + vb), 2.0);
            }
            
            // make sharp edges
            v1 = 1.0 - smoothstep(0.0, 0.3, v1);
            
            // noise is used as intensity map
            v2 = a * (noise1(v1 * 5.5 + 0.1));
            
            // octave 0's intensity changes a bit
            if(i == 0)
            v += v2 * flicker;
            else
            v += v2;
            
            f *= 3.0;
            a *= 0.7;
        }
        
        // slight vignetting
        v *= exp(-0.6 * length(suv)) * 1.2;
        
        // use texture channel0 for color? why not.
        vec3 cexp = texture2D(iChannel0, uv * 0.001).xyz * 3.0 + texture2D(iChannel0, uv * 0.01).xyz;//vec3(1.0, 2.0, 4.0);
        cexp *= 1.4;
        
        // old blueish color set
        //vec3 cexp = vec3(6.0, 4.0, 2.0);
        
        vec3 col = vec3(pow(v, cexp.x), pow(v, cexp.y), pow(v, cexp.z)) * 2.0;
        
        gl_FragColor = vec4(col, 1.0);
    }

</script>
<script>

  var width = window.innerWidth; // 画布的宽度
  var height = window.innerHeight; // 画布的高度

  // 渲染器
  var renderer = new THREE.WebGLRenderer({antialias: true});
  renderer.setSize(width, height);
  // 将canvas添加到指定元素
  var element = document.getElementById('container');
  element.appendChild(renderer.domElement);

  // 场景
  var scene = new THREE.Scene();
  // 摄像机
  var camera = new THREE.PerspectiveCamera(45, width / height, 1, 2000);
  camera.position.set(0, 0, 360);
  scene.add(camera);
  var textureLoader = new THREE.TextureLoader();

  //对纹理进行平铺
  var lavaTexture = THREE.ImageUtils.loadTexture( './data/noise.png' );
  lavaTexture.wrapS = lavaTexture.wrapT = THREE.RepeatWrapping;
  lavaTexture.repeat.set( 200, 200 );
  
  
  // 传递给着色器的uniform参数
  var uniforms = {
    iTime: {value: 1.0},
    iResolution: {value: new THREE.Vector2(width * 1.0, height * 1.0)},
    iChannel0: { value: lavaTexture },

  }
  // 材质
  var material = new THREE.ShaderMaterial({
    // 着色器 uniform 参数
    uniforms: uniforms,
    // 顶点着色器文本内容
    vertexShader: document.getElementById('vertexShader').textContent,
    // 片元着色器文本内容
    fragmentShader: document.getElementById('fragmentShader').textContent
  });
  // 平面几何体
  var geom = new THREE.PlaneBufferGeometry(width, height);
  // 网格对象
  var mesh = new THREE.Mesh(geom, material);
  scene.add(mesh);


  /* 利用requestAnimationFrame实现动画 */
  var clock = new THREE.Clock(); // 时钟
  (function animate() {
    requestAnimationFrame(animate);

    var delta = clock.getDelta(); // 获取本次和上次的时间间隔
    uniforms.iTime.value += delta/6; // 设置着色器使用的 iTime 参数
    renderer.render(scene, camera); // 重新渲染
  })();

  /* 监听鼠标移动，并改变着色器使用的 iMouse 参数 */
  var mouseStartPosition = null; // 鼠标起始位置
  window.addEventListener("mousemove", function (event) {
    if (!mouseStartPosition) {
      mouseStartPosition = {x: event.clientX, y: event.clientY}
    } else {
      uniforms.iMouse.value.x = event.clientX - mouseStartPosition.x;
      uniforms.iMouse.value.y = event.clientY - mouseStartPosition.y;
    }
  })
</script>
</body>

</html>